namespace IO {
  def readFile(path string) string
  def writeFile(path string, contents string) bool

  if TARGET == .JAVASCRIPT {
    def readFile(path string) string {
      try {
        var contents string = dynamic.require("fs").readFileSync(path, "utf8")
        return contents.replaceAll("\r\n", "\n")
      }
      return null
    }

    def writeFile(path string, contents string) bool {
      try {
        dynamic.require("fs").writeFileSync(path, contents)
        return true
      }
      return false
    }
  }

  else if TARGET == .CSHARP {
    def readFile(path string) string {
      try {
        var contents string = dynamic.System.IO.File.ReadAllText(path)
        return contents.replaceAll("\r\n", "\n")
      }
      return null
    }

    def writeFile(path string, contents string) bool {
      try {
        dynamic.System.IO.File.WriteAllText(path, contents)
        return true
      }
      return false
    }
  }
}
