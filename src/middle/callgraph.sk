namespace skew {
  class CallInfo {
    const symbol Symbol
    const callSites List<Node> = []
  }

  class CallGraph {
    const callInfo List<CallInfo> = []
    const symbolToInfoIndex IntMap<int> = {}

    def new(global ObjectSymbol) {
      # TODO: These should be inserted automatically
      callInfo = []
      symbolToInfoIndex = {}

      visitObject(global)
    }

    @private {
      def visitObject(symbol ObjectSymbol) {
        for object in symbol.objects {
          visitObject(object)
        }

        for function in symbol.functions {
          recordCallSite(function, null)
          if function.block != null {
            visitNode(function.block)
          }
        }

        for variable in symbol.variables {
          if variable.value != null {
            visitNode(variable.value)
          }
        }
      }

      def visitNode(node Node) {
        if node.children != null {
          for child in node.children {
            if child != null {
              visitNode(child)
            }
          }
        }

        if node.kind == .CALL && node.symbol != null {
          assert(node.symbol.kind.isFunction)
          recordCallSite(node.symbol, node)
        }

        else if node.kind == .VAR {
          var variable = node.symbol.asVariableSymbol
          if variable.value != null {
            visitNode(variable.value)
          }
        }

        else if node.kind == .LAMBDA {
          var function = node.symbol.asFunctionSymbol
          if function.block != null {
            visitNode(function.block)
          }
        }
      }

      def recordCallSite(symbol Symbol, node Node) {
        var index = symbolToInfoIndex.get(symbol.id, -1)
        var info = index < 0 ? CallInfo.new(symbol) : callInfo[index]
        if index < 0 {
          symbolToInfoIndex[symbol.id] = callInfo.count
          callInfo.append(info)
        }
        if node != null {
          info.callSites.append(node)
        }
      }
    }
  }
}
