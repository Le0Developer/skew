namespace skew {
  enum ScopeKind {
    FUNCTION
    LOCAL
    TYPE
  }

  class Scope {
    Scope parent

    virtual ScopeKind kind()
    virtual Symbol find(string name)

    TypeScope asType() {
      assert kind() == .TYPE
      return (TypeScope)this
    }

    FunctionScope asFunction() {
      assert kind() == .FUNCTION
      return (FunctionScope)this
    }

    LocalScope asLocal() {
      assert kind() == .LOCAL
      return (LocalScope)this
    }

    FunctionScope findEnclosingFunction() {
      for (var scope = this; scope != null; scope = scope.parent) {
        if (scope.kind() == .FUNCTION) {
          return scope.asFunction()
        }
      }
      return null
    }
  }

  class TypeScope : Scope {
    TypeSymbol symbol

    override ScopeKind kind() {
      return .TYPE
    }

    override Symbol find(string name) {
      var result = symbol.members.getOrDefault(name, null)
      return result != null ? result : parent != null ? parent.find(name) : null
    }
  }

  class FunctionScope : Scope {
    FunctionSymbol symbol
    StringMap<ParameterSymbol> parameters = {}

    override ScopeKind kind() {
      return .FUNCTION
    }

    override Symbol find(string name) {
      var result = parameters.getOrDefault(name, null)
      return result != null ? result : parent != null ? parent.find(name) : null
    }
  }

  class LocalScope : Scope {
    StringMap<VariableSymbol> locals = {}

    override ScopeKind kind() {
      return .LOCAL
    }

    override Symbol find(string name) {
      var result = locals.getOrDefault(name, null)
      return result != null ? result : parent != null ? parent.find(name) : null
    }

    void define(VariableSymbol symbol, Log log) {
      symbol.scope = this

      // Check for duplicates
      var other = locals.getOrDefault(symbol.name, null)
      if (other != null) {
        log.semanticErrorDuplicateSymbol(symbol.range, symbol.name, other.range)
        return
      }

      // Check for shadowing
      for (var scope = parent; scope.kind() == .LOCAL; scope = scope.parent) {
        var other = scope.asLocal().locals.getOrDefault(symbol.name, null)
        if (other != null) {
          log.semanticErrorShadowedSymbol(symbol.range, symbol.name, other.range)
          return
        }
      }

      locals[symbol.name] = symbol
    }
  }
}
