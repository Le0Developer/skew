namespace astral {
  using astral.merging

  void mergingPass(Log log, TypeSymbol global) {
    mergeType(log, null, global, global)
  }
}

namespace astral.merging {
  void mergeType(Log log, TypeSymbol parent, TypeSymbol target, TypeSymbol symbol) {
    target.scope = TypeScope(parent != null ? parent.scope : null, target)
    symbol.parent = parent

    if (symbol.parameters != null) {
      for (var i = 0; i < symbol.parameters.size(); i++) {
        var parameter = symbol.parameters[i]
        parameter.scope = parent.scope
        parameter.parent = target

        // Type parameters cannot merge with any members
        var other = target.members.getOrDefault(parameter.name, null)
        if (other != null) {
          log.semanticErrorDuplicateSymbol(parameter.range, parameter.name, other.range)
          continue
        }

        target.members[parameter.name] = parameter
      }
    }

    mergeTypes(log, target, symbol.types)
    mergeFunctions(log, target, symbol.functions)
    mergeVariables(log, target, symbol.variables)
  }

  void mergeTypes(Log log, TypeSymbol parent, List<TypeSymbol> children) {
    var members = parent.members
    var n = children.size()
    var count = 0

    for (var i = 0; i < n; i++) {
      var child = children[i]
      var other = members.getOrDefault(child.name, null)

      // Simple case: no merging
      if (other == null) {
        members[child.name] = child
        children[count] = child
        count++
        mergeType(log, parent, child, child)
        continue
      }

      // Can only merge with another of the same type or with a namespace
      if (other.kind == .TYPE_NAMESPACE) {
        other.kind = child.kind
      } else if (child.kind != .TYPE_NAMESPACE && child.kind != other.kind) {
        log.semanticErrorDuplicateSymbol(child.range, child.name, other.range)
        continue
      }

      // Classes can only have one base type
      var type = (TypeSymbol)other
      if (child.base != null && type.base != null) {
        log.semanticErrorDuplicateBaseType(child.base.range, child.name, type.base.range)
        continue
      }
      if (child.base != null) {
        type.base = child.base
      }

      // Cannot merge two types that both have type parameters
      if (child.parameters != null && type.parameters != null) {
        log.semanticErrorDuplicateTypeParameters(child.range, child.name, type.range)
        continue
      }

      // Merge "child" into "other"
      mergeType(log, parent, type, child)
      type.types.pushAll(child.types)
      type.functions.pushAll(child.functions)
      type.variables.pushAll(child.variables)
      if (child.parameters != null) {
        type.parameters = child.parameters
      }
    }

    // Remove merged declarations using O(n), would be O(n^2) if removeAt() was used
    while (n > count) {
      children.pop()
      n--
    }
  }

  void mergeFunctions(Log log, TypeSymbol parent, List<FunctionSymbol> children) {
    var members = parent.members

    for (var i = 0, n = children.size(); i < n; i++) {
      var child = children[i]
      var other = members.getOrDefault(child.name, null)
      var scope = FunctionScope(parent.scope, child)
      child.scope = scope
      child.parent = parent

      if (child.parameters != null) {
        for (var i = 0; i < child.parameters.size(); i++) {
          var parameter = child.parameters[i]
          parameter.scope = scope
          parameter.parent = child

          // Type parameters cannot merge with other parameters on this function
          var previous = scope.parameters.getOrDefault(parameter.name, null)
          if (previous != null) {
            log.semanticErrorDuplicateSymbol(parameter.range, parameter.name, previous.range)
            continue
          }

          scope.parameters[parameter.name] = parameter
        }
      }

      // Simple case: no merging
      if (other == null) {
        members[child.name] = child
        continue
      }

      // Merge with another symbol of the same type
      if (child.kind == other.kind) {
        var overloaded = OverloadedFunctionSymbol(child.kind.toOverloadedFunction(), child.name, [(FunctionSymbol)other, child])
        members[child.name] = overloaded
        ((FunctionSymbol)other).overloaded = overloaded
        child.overloaded = overloaded
        overloaded.scope = parent.scope
        overloaded.parent = parent
        continue
      }

      // Merge with a group of overloaded functions
      if (other.kind.isOverloadedFunction() && child.kind == other.kind.fromOverloadedFunction()) {
        ((OverloadedFunctionSymbol)other).symbols.push(child)
        child.overloaded = (OverloadedFunctionSymbol)other
        continue
      }

      log.semanticErrorDuplicateSymbol(child.range, child.name, other.range)
    }
  }

  void mergeVariables(Log log, TypeSymbol parent, List<VariableSymbol> children) {
    var members = parent.members

    for (var i = 0, n = children.size(); i < n; i++) {
      var child = children[i]
      var other = members.getOrDefault(child.name, null)
      child.scope = parent.scope
      child.parent = parent

      // Variables never merge
      if (other != null) {
        log.semanticErrorDuplicateSymbol(child.range, child.name, other.range)
        continue
      }

      members[child.name] = child
    }
  }
}
