namespace skew {
  def renamingPass(global ObjectSymbol) {
    renaming.renameObject(global)
    renaming.useOverriddenNames(global) # Use a second pass to avoid ordering issues
  }
}

namespace skew.renaming {
  def renameObject(symbol ObjectSymbol) {
    for object in symbol.objects {
      renameObject(object)
    }

    for function in symbol.functions {
      if !function.isImportedOrExported && function.overridden == null {
        var scope = function.scope.parent
        var count = function.arguments.count

        if (count == 0 || count == 1 && function.kind == .FUNCTION_GLOBAL) && function.name in unaryPrefixes {
          function.name = scope.generateSymbolName(unaryPrefixes[function.name])
        }

        else if function.name in prefixes {
          function.name = scope.generateSymbolName(prefixes[function.name])
        }

        else if isInvalidIdentifier(function) {
          function.name = scope.generateSymbolName("_")
        }

        else if function.overloaded != null && function.overloaded.symbols.count > 1 {
          function.name = scope.generateSymbolName(function.name)
        }
      }
    }
  }

  def useOverriddenNames(symbol ObjectSymbol) {
    for object in symbol.objects {
      useOverriddenNames(object)
    }

    for function in symbol.functions {
      if function.overridden != null {
        var overridden = function.overridden
        while overridden.overridden != null {
          overridden = overridden.overridden
        }
        function.name = overridden.name
      }
    }
  }

  def isAlpha(c int) bool {
    return c >= 'a' && c <= 'z' || c >= 'A' && c <= 'Z' || c == '_'
  }

  def isNumber(c int) bool {
    return c >= '0' && c <= '9'
  }

  def isInvalidIdentifier(symbol Symbol) bool {
    for i in 0..symbol.name.count {
      var c = symbol.name[i]
      if !isAlpha(c) && (i == 0 || !isNumber(c)) {
        return true
      }
    }
    return false
  }

  const unaryPrefixes = {
    "!": "not",
    "+": "positive",
    "++": "increment",
    "-": "negative",
    "--": "decrement",
    "~": "complement",
  }

  const prefixes = {
    "%": "remainder",
    "&": "and",
    "*": "multiply",
    "**": "power",
    "+": "add",
    "-": "subtract",
    "/": "divide",
    "<<": "leftShift",
    "<=>": "compare",
    ">>": "rightShift",
    "^": "xor",
    "|": "or",
    "in": "contains",

    "%=": "remainderUpdate",
    "&=": "andUpdate",
    "**=": "powerUpdate",
    "*=": "multiplyUpdate",
    "+=": "addUpdate",
    "-=": "subtractUpdate",
    "/=": "divideUpdate",
    "<<=": "leftShiftUpdate",
    ">>=": "rightShiftUpdate",
    "^=": "xorUpdate",
    "|=": "orUpdate",

    "[]": "get",
    "[]=": "set",

    "[...]": "append",
    "[new]": "new",
    "{...}": "insert",
    "{new}": "new",
  }
}
