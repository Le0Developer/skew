namespace skew {
  class LineColumn {
    var line int # 0-based index
    var column int # 0-based index
  }

  class Source {
    var name string
    var contents string

    # This maps line numbers to indices within contents
    @private
    var lineOffsets List<int> = null

    def lineCount int {
      computeLineOffsets
      return lineOffsets.count - 1 # Ignore the line offset at 0
    }

    def contentsOfLine(line int) string {
      computeLineOffsets
      if line < 0 || line >= lineOffsets.count {
        return ""
      }
      var start = lineOffsets[line]
      var end = line + 1 < lineOffsets.count ? lineOffsets[line + 1] - 1 : contents.count
      return contents.slice(start, end)
    }

    def indexToLineColumn(index int) LineColumn {
      computeLineOffsets

      # Binary search to find the line
      var count = lineOffsets.count
      var line = 0
      while count > 0 {
        var step = count / 2
        var i = line + step
        if lineOffsets[i] <= index {
          line = i + 1
          count = count - step - 1
        } else {
          count = step
        }
      }

      # Use the line to compute the column
      var column = line > 0 ? index - lineOffsets[line - 1] : index
      return LineColumn.new(line - 1, column)
    }

    @private
    def computeLineOffsets {
      if lineOffsets == null {
        lineOffsets = [0]
        for i in 0..contents.count {
          if contents[i] == '\n' {
            lineOffsets.append(i + 1)
          }
        }
      }
    }
  }
}
