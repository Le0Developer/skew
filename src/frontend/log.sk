namespace astral {
  enum DiagnosticKind {
    ERROR
    WARNING
  }

  class Diagnostic {
    DiagnosticKind kind
    Range range
    string text
    Range noteRange = null
    var noteText = ""

    static string format(string kind, Range range, string text) {
      if (range == null) {
        return kind + ": " + text + "\n"
      }
      var formatted = range.format(0)
      return range.locationString() + ": " + kind + ": " + text + "\n" + formatted.line + "\n" + formatted.range + "\n"
    }
  }

  class Log {
    List<Diagnostic> diagnostics = []
    var warningCount = 0
    var errorCount = 0

    string toString() {
      var builder = StringBuilder()

      // Emit the log assuming an infinite terminal width
      for (var i = 0; i < diagnostics.size(); i++) {
        var diagnostic = diagnostics[i]
        builder.append(Diagnostic.format(diagnostic.kind == .ERROR ? "error" : "warning", diagnostic.range, diagnostic.text))

        // Append notes after the diagnostic they apply to
        if (diagnostic.noteRange != null) {
          builder.append(Diagnostic.format("note", diagnostic.noteRange, diagnostic.noteText))
        }
      }

      return builder.toString()
    }

    bool isEmpty() {
      return diagnostics.size() == 0
    }

    bool hasErrors() {
      return errorCount != 0
    }

    bool hasWarnings() {
      return warningCount != 0
    }

    void error(Range range, string text) {
      diagnostics.push(Diagnostic(.ERROR, range, text))
      errorCount++
    }

    void warning(Range range, string text) {
      diagnostics.push(Diagnostic(.WARNING, range, text))
      warningCount++
    }

    void note(Range range, string text) {
      var last = diagnostics.last()
      last.noteRange = range
      last.noteText = text
    }
  }

  in Log {
    void syntaxErrorInvalidEscapeSequence(Range range) {
      error(range, "Invalid escape sequence")
    }

    void syntaxErrorExtraData(Range range, string text) {
      error(range, "Syntax error \"" + text + "\"")
    }

    void syntaxErrorUnexpectedToken(Token token) {
      error(token.range, "Unexpected " + token.kind)
    }

    void syntaxErrorExpectedToken(Range range, TokenKind found, TokenKind expected) {
      error(range, "Expected " + expected + " but found " + found)
    }
  }

  in Log {
    void semanticErrorDuplicateSymbol(Range range, string name, Range previous) {
      error(range, "\"" + name + "\" is already declared")
      if (previous != null) {
        note(previous, "The previous declaration is here")
      }
    }

    void semanticErrorDuplicateBaseType(Range range, string name, Range previous) {
      error(range, "\"" + name + "\" already has a base type")
      if (previous != null) {
        note(previous, "The previous base type is here")
      }
    }

    void semanticErrorCyclicDeclaration(Range range, string name) {
      error(range, "Cyclic declaration of \"" + name + "\"")
    }

    void semanticErrorUndeclaredSymbol(Range range, string name) {
      error(range, "\"" + name + "\" is not declared")
    }
  }
}
