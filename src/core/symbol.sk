namespace skew {
  enum SymbolKind {
    NONE
    PARAMETER

    TYPE_CLASS
    TYPE_GLOBAL
    TYPE_INTERFACE
    TYPE_NAMESPACE

    FUNCTION_CONSTRUCTOR
    FUNCTION_GLOBAL
    FUNCTION_INSTANCE
    FUNCTION_LOCAL

    OVERLOADED_GLOBAL
    OVERLOADED_INSTANCE

    VARIABLE_GLOBAL
    VARIABLE_INSTANCE
    VARIABLE_LOCAL
  }

  in SymbolKind {
    bool isType() {
      return this >= TYPE_CLASS && this <= TYPE_NAMESPACE
    }

    bool isFunction() {
      return this >= FUNCTION_CONSTRUCTOR && this <= FUNCTION_LOCAL
    }

    bool isOverloadedFunction() {
      return this >= OVERLOADED_GLOBAL && this <= OVERLOADED_INSTANCE
    }

    bool isFunctionOrOverloadedFunction() {
      return this >= FUNCTION_CONSTRUCTOR && this <= OVERLOADED_INSTANCE
    }

    bool isVariable() {
      return this >= VARIABLE_GLOBAL && this <= VARIABLE_LOCAL
    }

    bool isOverloadableFunction() {
      return this >= FUNCTION_CONSTRUCTOR && this <= FUNCTION_INSTANCE
    }

    bool hasInstances() {
      return this == TYPE_CLASS || this == TYPE_INTERFACE
    }

    bool isOnInstances() {
      return this == FUNCTION_INSTANCE || this == VARIABLE_INSTANCE || this == OVERLOADED_INSTANCE
    }
  }

  enum flags SymbolFlags {
    VALUE_TYPE
    GETTER
    SETTER
  }

  enum SymbolState {
    UNINITIALIZED
    INITIALIZING
    INITIALIZED
  }

  class Symbol {
    final int id = createID()
    SymbolKind kind
    string name
    Range range = null // The location of the name in the source code
    Symbol parent = null // Automatically set by the merging step
    Type resolvedType = null // Automatically set by the resolving step
    Scope scope = null // Automatically set by the merging step (resolving step for local variables)
    SymbolState state = .UNINITIALIZED
    SymbolFlags flags = 0
    List<string> comments = null

    ParameterSymbol asParameter() {
      assert kind == .PARAMETER
      return (ParameterSymbol)this
    }

    TypeSymbol asType() {
      assert kind.isType()
      return (TypeSymbol)this
    }

    FunctionSymbol asFunction() {
      assert kind.isFunction()
      return (FunctionSymbol)this
    }

    OverloadedFunctionSymbol asOverloadedFunction() {
      assert kind.isOverloadedFunction()
      return (OverloadedFunctionSymbol)this
    }

    VariableSymbol asVariable() {
      assert kind.isVariable()
      return (VariableSymbol)this
    }

    string fullName() {
      if (parent != null && parent.kind != .TYPE_GLOBAL && kind != .PARAMETER) {
        return parent.fullName() + "." + name
      }
      return name
    }

    bool isValueType() {
      return (flags & .VALUE_TYPE) != 0
    }

    bool isGetter() {
      return (flags & .GETTER) != 0
    }

    bool isSetter() {
      return (flags & .SETTER) != 0
    }

    virtual List<ParameterSymbol> typeParameters() {
      return null
    }

    private static {
      int createID() {
        nextID++
        return nextID
      }

      int nextID = 0
    }
  }

  class ParameterSymbol : Symbol {
  }

  class TypeSymbol : Symbol {
    Node base = null
    StringMap<Symbol> members = {}
    List<TypeSymbol> types = []
    List<FunctionSymbol> functions = []
    List<VariableSymbol> variables = []
    List<ParameterSymbol> parameters = null

    override List<ParameterSymbol> typeParameters() {
      return parameters
    }
  }

  class FunctionSymbol : Symbol {
    OverloadedFunctionSymbol overloaded = null
    List<ParameterSymbol> parameters = null
    List<VariableSymbol> arguments = []
    VariableSymbol self = null // For instance functions and constructors
    Node returnType = null
    Node block = null

    override List<ParameterSymbol> typeParameters() {
      return parameters
    }
  }

  class VariableSymbol : Symbol {
    Node type = null
    Node value = null
  }

  class OverloadedFunctionSymbol : Symbol {
    List<FunctionSymbol> symbols
  }
}
