namespace skew {
  class JsEmitter : Emitter {
    override void visit(ObjectSymbol global) {
      var objects = sortedObjects(global)
      for (var i = 0; i < objects.size(); i++) {
        emitObject(objects[i])
      }
      createSource("out.js")
    }

    private {
      Symbol previousSymbol = null
      var prefix = ""

      void emitNewlineBeforeSymbol(Symbol symbol) {
        if (previousSymbol != null &&
            (!previousSymbol.kind.isObject() || !symbol.kind.isObject() || symbol.comments != null) &&
            (!previousSymbol.kind.isVariable() || !symbol.kind.isVariable() || symbol.comments != null)) {
          previousSymbol = null
          emit("\n")
        }
      }

      void emitNewlineAfterSymbol(Symbol symbol) {
        previousSymbol = symbol
      }

      void emitComments(List<string> comments) {
        if (comments != null) {
          for (var i = 0; i < comments.size(); i++) {
            emit(indent + "//" + comments[i])
          }
        }
      }

      static string computePrefix(ObjectSymbol symbol) {
        assert symbol.kind.isObject()
        return symbol.kind == .OBJECT_GLOBAL ? "" : computePrefix(symbol.parent.asObjectSymbol()) + symbol.name + "."
      }

      void emitObject(ObjectSymbol symbol) {
        if (symbol.isImported()) {
          return
        }

        prefix = symbol.parent != null ? computePrefix(symbol.parent.asObjectSymbol()) : ""

        switch (symbol.kind) {
          case .OBJECT_NAMESPACE, .OBJECT_INTERFACE {
            emitNewlineBeforeSymbol(symbol)
            emitComments(symbol.comments)
            emit(indent + (prefix == "" ? "var " : prefix) + symbol.name + " = {};\n")
            emitNewlineAfterSymbol(symbol)
          }

          case .OBJECT_CLASS {
            for (var i = 0; i < symbol.functions.size(); i++) {
              var function = symbol.functions[i]
              if (function.kind == .FUNCTION_CONSTRUCTOR) {
                if (function.comments == null && symbol.comments != null) {
                  function.comments = symbol.comments
                }
                emitFunction(function)
              }
            }
          }
        }

        if (symbol.kind != .OBJECT_GLOBAL) {
          prefix += symbol.name + "."
        }

        for (var i = 0; i < symbol.functions.size(); i++) {
          var function = symbol.functions[i]
          if (function.kind != .FUNCTION_CONSTRUCTOR) {
            emitFunction(function)
          }
        }

        for (var i = 0; i < symbol.variables.size(); i++) {
          emitVariable(symbol.variables[i])
        }
      }

      void emitArgumentList(List<VariableSymbol> arguments) {
        for (var i = 0; i < arguments.size(); i++) {
          if (i > 0) emit(", ")
          emit(arguments[i].name)
        }
      }

      void emitFunction(FunctionSymbol symbol) {
        if (symbol.block == null) {
          return
        }

        emitNewlineBeforeSymbol(symbol)
        emitComments(symbol.comments)
        var isExpression = prefix != ""
        var name = symbol.kind == .FUNCTION_CONSTRUCTOR ? symbol.parent.name : symbol.name
        if (isExpression) {
          emit(prefix + (symbol.kind == .FUNCTION_INSTANCE ? "prototype." : "") + name + " = function(")
        } else {
          emit("function " + name + "(")
        }
        emitArgumentList(symbol.arguments)
        emit(")")
        emitBlock(symbol.block)
        emit(isExpression ? ";\n" : "\n")
        emitNewlineAfterSymbol(symbol)
      }

      void emitVariable(VariableSymbol symbol) {
        if (symbol.isImported()) {
          return
        }

        if (symbol.kind != .VARIABLE_INSTANCE && (symbol.value != null || prefix == "")) {
          emitNewlineBeforeSymbol(symbol)
          emitComments(symbol.comments)
          emit(indent + (prefix == "" || symbol.kind == .VARIABLE_LOCAL ? "var " : prefix) + symbol.name)
          if (symbol.value != null) {
            emit(" = ")
            emitExpression(symbol.value, .LOWEST)
          }
          emit(";\n")
          emitNewlineAfterSymbol(symbol)
        }
      }

      void emitBlock(Node node) {
        emit(" {\n")
        increaseIndent()
        for (var i = 0; i < node.children.size(); i++) {
          var statement = node.children[i]
          emitComments(statement.comments)
          emitStatement(statement)
        }
        decreaseIndent()
        emit(indent + "}")
      }

      void emitStatement(Node node) {
        switch (node.kind) {
          case .VAR {
            emitVariable((VariableSymbol)node.symbol)
          }

          case .EXPRESSION {
            emit(indent)
            emitExpression(node.expressionValue(), .LOWEST)
            emit(";\n")
          }

          case .BREAK {
            emit(indent + "break;\n")
          }

          case .CONTINUE {
            emit(indent + "continue;\n")
          }

          case .IMPLICIT_RETURN, .RETURN {
            emit(indent + "return")
            var value = node.returnValue()
            if (value != null) {
              emit(" ")
              emitExpression(value, .LOWEST)
            }
            emit(";\n")
          }

          case .IF {
            emit(indent + "if (")
            emitExpression(node.ifTest(), .LOWEST)
            emit(")")
            emitBlock(node.ifTrue())
            var block = node.ifFalse()
            if (block != null) {
              emit(" else")
              emitBlock(block)
            }
            emit("\n")
          }

          case .WHILE {
            emit(indent + "while (")
            emitExpression(node.whileTest(), .LOWEST)
            emit(")")
            emitBlock(node.whileBlock())
            emit("\n")
          }

          default {
            assert false
          }
        }
      }

      void emitExpression(Node node, Precedence precedence) {
        switch (node.kind) {
          case .NULL {
            emit("null")
          }

          case .NAME {
            emit(node.symbol != null ? node.symbol.fullName() : node.asString())
          }

          case .DOT {
            emitExpression(node.dotTarget(), .MEMBER)
            emit("." + (node.symbol != null ? node.symbol.name : node.asString()))
          }

          case .CONSTANT {
            var value = node.content
            switch (value.kind()) {
              case .BOOL { emit(value.asBool().toString()) }
              case .INT { emit(value.asInt().toString()) }
              case .DOUBLE { emit(value.asDouble().toString()) }
              case .STRING { emit(quoteString(value.asString(), '"')) }
            }
          }

          case .CALL {
            var value = node.callValue()
            var wrap = value.kind == .LAMBDA && node.parent.kind == .EXPRESSION
            if (wrap) emit("(")
            emitExpression(value, .UNARY_POSTFIX)
            if (wrap) emit(")")
            emit("(")
            for (var i = 1; i < node.children.size(); i++) {
              if (i > 1) {
                emit(", ")
              }
              emitExpression(node.children[i], .LOWEST)
            }
            emit(")")
          }

          case .INDEX {
            emitExpression(node.children[0], .UNARY_POSTFIX)
            emit("[")
            for (var i = 1; i < node.children.size(); i++) {
              if (i > 1) {
                emit(", ")
              }
              emitExpression(node.children[i], .LOWEST)
            }
            emit("]")
          }

          case .ASSIGN_INDEX {
            var count = node.children.size()
            if (.ASSIGN < precedence) emit("(")
            emitExpression(node.children[0], .UNARY_POSTFIX)
            emit("[")
            for (var i = 1; i + 1 < count; i++) {
              if (i > 1) {
                emit(", ")
              }
              emitExpression(node.children[i], .LOWEST)
            }
            emit("] = ")
            emitExpression(node.children[count - 1], .LOWEST)
            if (.ASSIGN < precedence) emit(")")
          }

          case .CAST {
            emitExpression(node.castValue(), precedence)
          }

          case .PARAMETERIZE {
            emitExpression(node.parameterizeValue(), precedence)
          }

          case .HOOK {
            if (.ASSIGN < precedence) emit("(")
            emitExpression(node.hookTest(), .LOGICAL_OR)
            emit(" ? ")
            emitExpression(node.hookTrue(), .ASSIGN)
            emit(" : ")
            emitExpression(node.hookFalse(), .ASSIGN)
            if (.ASSIGN < precedence) emit(")")
          }

          case .LAMBDA {
            var symbol = (FunctionSymbol)node.symbol
            emit("function(")
            emitArgumentList(symbol.arguments)
            emit(")")
            emitBlock(symbol.block)
          }

          default {
            if (node.kind.isUnary()) {
              var value = node.unaryValue()
              var info = operatorInfo[node.kind]
              if (info.precedence < precedence) emit("(")
              emit(info.text)
              emitExpression(value, info.precedence)
              if (info.precedence < precedence) emit(")")
            }

            else if (node.kind.isBinary()) {
              var info = operatorInfo[node.kind]
              if (info.precedence < precedence) emit("(")
              emitExpression(node.binaryLeft(), info.precedence.incrementIfRightAssociative(info.associativity))
              emit(" " + info.text + " ")
              emitExpression(node.binaryRight(), info.precedence.incrementIfLeftAssociative(info.associativity))
              if (info.precedence < precedence) emit(")")
            }

            else {
              assert false
            }
          }
        }
      }
    }
  }
}
