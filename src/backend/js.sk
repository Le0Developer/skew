namespace astral {
  class JsEmitter : Emitter {
    string dump(TypeSymbol global) {
      emitType(global)
      createSource("out.js")
      return sources()[0].contents
    }

    private {
      var prefix = ""

      void emitType(TypeSymbol symbol) {
        var old = prefix

        if (symbol.kind == .TYPE_NAMESPACE) {
          emit(indent + (prefix == "" ? "var " : prefix) + symbol.name + " = {};\n")
        }

        if (symbol.kind != .TYPE_GLOBAL) {
          prefix += symbol.name + "."
        }

        for (var i = 0; i < symbol.types.size(); i++) {
          emitType(symbol.types[i])
        }

        for (var i = 0; i < symbol.funcs.size(); i++) {
          emitFunc(symbol.funcs[i])
        }

        for (var i = 0; i < symbol.vars.size(); i++) {
          emitVar(symbol.vars[i])
        }

        prefix = old
      }

      void emitArgumentList(List<VarSymbol> args) {
        for (var i = 0; i < args.size(); i++) {
          if (i > 0) emit(", ")
          emit(args[i].name)
        }
      }

      void emitFunc(FuncSymbol symbol) {
        if (symbol.block == null) {
          return
        }
        var isExpression = prefix != ""
        if (isExpression) {
          emit(prefix + (symbol.kind == .FUNC_INSTANCE ? "prototype." : "") + symbol.name + " = function(")
        } else {
          emit("function " + symbol.name + "(")
        }
        emitArgumentList(symbol.args)
        emit(")")
        emitBlock(symbol.block)
        emit(isExpression ? ";\n" : "\n")
      }

      void emitVar(VarSymbol symbol) {
        if (symbol.kind != .VAR_INSTANCE) {
          emit(indent + (prefix == "" ? "var " : prefix) + symbol.name)
          if (symbol.value != null) {
            emit(" = ")
            emitExpression(symbol.value, .LOWEST)
          }
          emit(";\n")
        }
      }

      void emitBlock(Node node) {
        emit(" {\n")
        increaseIndent()
        for (var i = 0; i < node.children.size(); i++) {
          emitStatement(node.children[i])
        }
        decreaseIndent()
        emit(indent + "}")
      }

      void emitStatement(Node node) {
        switch (node.kind) {
          case .VAR {
            emitVar((VarSymbol)node.symbol)
          }

          case .EXPRESSION {
            emit(indent)
            emitExpression(node.expressionValue(), .LOWEST)
            emit(";\n")
          }

          case .BREAK {
            emit(indent + "break;\n")
          }

          case .CONTINUE {
            emit(indent + "continue;\n")
          }

          case .RETURN {
            emit(indent + "return")
            var value = node.returnValue()
            if (value != null) {
              emit(" ")
              emitExpression(value, .LOWEST)
            }
            emit(";\n")
          }

          case .IF {
            emit(indent + "if ")
            emitExpression(node.ifTest(), .LOWEST)
            emitBlock(node.ifTrue())
            var block = node.ifFalse()
            if (block != null) {
              emit(" else")
              emitBlock(block)
            }
            emit("\n")
          }

          case .WHILE {
            emit(indent + "while (")
            emitExpression(node.whileTest(), .LOWEST)
            emit(")")
            emitBlock(node.whileBlock())
            emit("\n")
          }

          default {
            assert false
          }
        }
      }

      void emitExpression(Node node, Precedence precedence) {
        switch (node.kind) {
          case .NULL {
            emit("null")
          }

          case .NAME {
            emit(node.asString())
          }

          case .DOT {
            emitExpression(node.dotTarget(), .MEMBER)
            emit("." + node.asString())
          }

          case .CONSTANT {
            var value = node.content
            switch (value.kind()) {
              case .BOOL { emit(value.asBool().toString()) }
              case .INT { emit(value.asInt().toString()) }
              case .DOUBLE { emit(value.asDouble().toString()) }
              case .STRING { emit(quoteString(value.asString(), '"')) }
            }
          }

          case .CALL {
            emitExpression(node.children[0], .UNARY_POSTFIX)
            emit("(")
            for (var i = 1; i < node.children.size(); i++) {
              if (i > 1) {
                emit(", ")
              }
              emitExpression(node.children[i], .LOWEST)
            }
            emit(")")
          }

          case .CAST {
            emitExpression(node.castValue(), precedence)
          }

          case .HOOK {
            if (.ASSIGN < precedence) emit("(")
            emitExpression(node.hookTest(), .LOGICAL_OR)
            emit(" ? ")
            emitExpression(node.hookTrue(), .ASSIGN)
            emit(" : ")
            emitExpression(node.hookFalse(), .ASSIGN)
            if (.ASSIGN < precedence) emit(")")
          }

          case .LAMBDA {
            var symbol = (FuncSymbol)node.symbol
            emit("function(")
            emitArgumentList(symbol.args)
            emit(")")
            emitBlock(symbol.block)
          }

          default {
            if (node.kind.isUnary()) {
              var value = node.unaryValue()
              var info = operatorInfo[node.kind]
              if (info.precedence < precedence) emit("(")
              emit(info.text)
              emitExpression(value, info.precedence)
              if (info.precedence < precedence) emit(")")
            }

            else if (node.kind.isBinary()) {
              var info = operatorInfo[node.kind]
              if (info.precedence < precedence) emit("(")
              emitExpression(node.binaryLeft(), info.precedence.incrementIfRightAssociative(info.associativity))
              emit(" " + info.text + " ")
              emitExpression(node.binaryRight(), info.precedence.incrementIfLeftAssociative(info.associativity))
              if (info.precedence < precedence) emit(")")
            }

            else {
              assert false
            }
          }
        }
      }
    }
  }
}
