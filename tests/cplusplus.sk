namespace Skew.Tests {
  def testCPlusPlus {

# Test entry point
test("
@entry
def test {}
", "
void main() {
}
").cpp

# Test entry point
test("
@entry
def test int {
  return 0
}
", "
int main() {
  return 0;
}
").cpp

# Test entry point
test("
@entry
def test(x List<string>) {
}
", "
void main(int argc, char** argv) {
  List<string> *x = new List<string>();

  if (*argv++) {
    while (*argv) {
      x->append(*argv++);
    }
  }
}
").cpp

# Test entry point
test("
@entry
def test(x List<string>) int {
  return x.count
}
", "
int main(int argc, char** argv) {
  List<string> *x = new List<string>();

  if (*argv++) {
    while (*argv) {
      x->append(*argv++);
    }
  }

  return x->count();
}
").cpp

# Test entry point inside a namespace
test("
namespace ns {
  @entry
  def test(x List<string>) int {
    test([])
    return x.count
  }
}
", "
int main(int argc, char** argv) {
  List<string> *x = new List<string>();

  if (*argv++) {
    while (*argv) {
      x->append(*argv++);
    }
  }

  main(new List<string>());
  return x->count();
}
").cpp

# Test entry point name collisions
test("
@entry
def main(x List<string>) int {
  var argc = 0
  var argv = 0
  return argc + argv
}
", "
int main(int argc1, char** argv1) {
  List<string> *x = new List<string>();

  if (*argv1++) {
    while (*argv1) {
      x->append(*argv1++);
    }
  }

  int argc = 0;
  int argv = 0;
  return argc + argv;
}
").cpp

# Basic class hierarchy
test("
@export
class Foo {
  const instanceVariable1 int
  const instanceVariable2 = 0
  def instanceMethod {}
  def instanceMethod2 {}
}

namespace Foo {
  const staticVariable = 0
}

@export
class Bar : Foo {
  const instanceVariable3 int
  const instanceVariable4 = 0
  over instanceMethod { super }
  def instanceMethod3 {}
}

namespace Bar {
  const staticVariable2 = 0
}
", "
struct Foo;
struct Bar;

struct Foo {
  virtual void instanceMethod();
  void instanceMethod2();
  Foo(int instanceVariable1);

  int instanceVariable1;
  int instanceVariable2;
  static int staticVariable;
};

struct Bar : Foo {
  virtual void instanceMethod() override;
  void instanceMethod3();
  Bar(int instanceVariable1, int instanceVariable3);

  int instanceVariable3;
  int instanceVariable4;
  static int staticVariable2;
};

int Foo::staticVariable = 0;
int Bar::staticVariable2 = 0;

void Foo::instanceMethod() {
}

void Foo::instanceMethod2() {
}

Foo::Foo(int instanceVariable1) {
  this->instanceVariable1 = instanceVariable1;
  this->instanceVariable2 = 0;
}

void Bar::instanceMethod() {
  Foo::instanceMethod();
}

void Bar::instanceMethod3() {
}

Bar::Bar(int instanceVariable1, int instanceVariable3) : Foo::Foo(instanceVariable1) {
  this->instanceVariable3 = instanceVariable3;
  this->instanceVariable4 = 0;
}
").cpp

# Basic interface usage
test("
@export
class Foo :: Bar, Baz {
  def instanceMethod {}
  def instanceMethod(x int) {}
  def instanceMethod2 {}
}

interface Bar {
  def instanceMethod
  def instanceMethod(x int)
}

interface Baz {
  def instanceMethod
  def instanceMethod2
}
", "
struct Bar;

struct Baz;

struct Foo;

struct Bar {
  virtual void instanceMethod() = 0;
  virtual void instanceMethod(int x) = 0;
};

struct Baz {
  virtual void instanceMethod() = 0;
  virtual void instanceMethod2() = 0;
};

struct Foo : Bar, Baz {
  virtual void instanceMethod();
  virtual void instanceMethod(int x);
  virtual void instanceMethod2();
  Foo();
};

void Foo::instanceMethod() {
}

void Foo::instanceMethod(int x) {
}

void Foo::instanceMethod2() {
}

Foo::Foo() {
}
").cpp

# Interface usage with tree shaking
test("
class Foo :: Bar, Baz {
  def instanceMethod {}
  def instanceMethod(x int) {}
  def instanceMethod2 {}
}

interface Bar {
  def instanceMethod
  def instanceMethod(x int)
}

interface Baz {
  def instanceMethod
  def instanceMethod2
}

@export
def test {
  var foo = Foo.new
  foo.instanceMethod
}
", "
struct Bar;

struct Baz;

struct Foo;

struct Bar {
  virtual void instanceMethod1() = 0;
};

struct Baz {
  virtual void instanceMethod1() = 0;
};

struct Foo : Bar, Baz {
  virtual void instanceMethod1();
  Foo();
};

void test() {
  Foo *foo = new Foo();
  foo->instanceMethod1();
}

void Foo::instanceMethod1() {
}

Foo::Foo() {
}
").cpp

# Type wrapping
test("
type Foo : double {
  def scaleBy(scale Foo) Foo {
    return ((self as double) * (scale as double)) as Foo
  }
}

namespace Foo {
  const FOO = 0.5 as Foo
}

@export
def test(x double) Foo {
  return (x as Foo).scaleBy(Foo.FOO)
}
", "
namespace Foo {
  double scaleBy(double self, double scale);
}

namespace Foo {
  double FOO = 0.5;
}

double test(double x) {
  return Foo::scaleBy(x, Foo::FOO);
}

double Foo::scaleBy(double self, double scale) {
  return self * scale;
}
").cpp

# Casting between enums and integers must be explicit
test("
enum Foo {
  FOO
}

@export
def test Foo {
  var x = Foo.FOO
  return ((x as int) * 1) as Foo
}
", "
enum struct Foo {
  FOO = 0,
};

Foo test() {
  Foo x = Foo::FOO;
  return (Foo)((int)x * 1);
}
").cpp

# Lists and maps
test("
@export
def foo {
  var x = [1, 2, 3]
  var y = {1: 2, 3: 4}
  var z = {\"1\": 2, \"3\": 4}
}
", "
#include <utility>

void foo() {
  List<int> *x = new List<int>({1, 2, 3});
  IntMap<int> *y = new IntMap<int>({std::make_pair(1, 2), std::make_pair(3, 4)});
  StringMap<int> *z = new StringMap<int>({std::make_pair(\"1\"_s, 2), std::make_pair(\"3\"_s, 4)});
}
").cpp

# Test math constants
test("
@export
def main {
  dynamic.foo(Math.NAN, Math.INFINITY, -Math.INFINITY)
  dynamic.foo(Math.NAN.toString, Math.INFINITY.toString, (-Math.INFINITY).toString)
}
", "
void main() {
  foo(0.0 / 0.0, 1.0 / 0.0, -(1.0 / 0.0));
  foo(doubleToString(0.0 / 0.0), doubleToString(1.0 / 0.0), doubleToString(-(1.0 / 0.0)));
}
").cpp.inlineAllFunctions

# Test math constants
test("
@export
def main {
  dynamic.foo(Math.NAN, Math.INFINITY, -Math.INFINITY)
  dynamic.foo(Math.NAN.toString, Math.INFINITY.toString, (-Math.INFINITY).toString)
}
", "
#include <math.h>

void main() {
  foo(NAN, INFINITY, -INFINITY);
  foo(doubleToString(NAN), doubleToString(INFINITY), doubleToString(-INFINITY));
}
").cpp.inlineAllFunctions.foldAllConstants

# Test math toString
test("
@export
def main {
  dynamic.foo(0.toString, 1.0.toString, (-1.0).toString, 0.5.toString, (-0.5).toString)
}
", "
void main() {
  foo(intToString(0), doubleToString(1.0), doubleToString(-1.0), doubleToString(0.5), doubleToString(-0.5));
}
").cpp.inlineAllFunctions

# Double literals must be emitted with a decimal point
test("
@export
def main(x double) {
  x = 1.0 / 2.0
  x = 1e100 / 2e100
  x = 1e-100 / 2e-100
  x = 1.5 / 2.5
  x = 1.5e100 / 2.5e100
  x = 1.5e-100 / 2.5e-100
}
", "
void main(double x) {
  x = 1.0 / 2.0;
  x = 1.0e+100 / 2.0e+100;
  x = 1.0e-100 / 2.0e-100;
  x = 1.5 / 2.5;
  x = 1.5e+100 / 2.5e+100;
  x = 1.5e-100 / 2.5e-100;
}
").cpp

# Check for a crash when converting switch statements to if chains
test("
@export
def main {
  switch \"a\" {
    case \"b\" {}
    case \"c\" {}
    default {}
  }
}
", "
void main() {
  string value = \"a\"_s;

  if (value == \"b\"_s) {
  }

  else if (value == \"c\"_s) {
  }

  else {
  }
}
").cpp

# Check different integer types
test("
enum Foo {
  A, B

  def foo {}
}

flags Bar {
  C, D

  def foo {}
}

type Baz : int {
  def foo {}
}

namespace Baz {
  const X = 0 as Baz
}

@export
def test int {
  var a = Foo.A
  var c = Bar.C
  var x = 0 as Baz
  a.foo
  c.foo
  x.foo
  return a + c + x as int + Foo.B + Bar.D + Baz.X as int
}
", "
enum struct Foo {
  A = 0,
  B = 1,
};

struct Bar {
  enum {
    C = 1,
    D = 2,
  };
};

namespace Baz {
  void foo(int self);
}

namespace in_Foo {
  void foo(Foo self);
}

namespace in_Bar {
  void foo(int self);
}

namespace Baz {
  int X = 0;
}

int test() {
  Foo a = Foo::A;
  int c = Bar::C;
  int x = 0;
  in_Foo::foo(a);
  in_Bar::foo(c);
  Baz::foo(x);
  return (int)a + c + x + (int)Foo::B + Bar::D + Baz::X;
}

void Baz::foo(int self) {
}

void in_Foo::foo(Foo self) {
}

void in_Bar::foo(int self) {
}
").cpp

# Check code generation for flags types
test("
flags Foo {
  X0
  X1
  X2
  X3
  X4
  X5
  X6
  X7
  X8
  X9

  X10
  X11
  X12
  X13
  X14
  X15
  X16
  X17
  X18
  X19

  X20
  X21
  X22
  X23
  X24
  X25
  X26
  X27
  X28
  X29

  X30
  X31
}

@export
def test {
  if !((.X0 | .X1) in (Foo.X30 | .X31)) {
    var x = Foo.X0
    x = .X1 | .X2
    x &= ~.X3
  }
}
", "
struct Foo {
  enum {
    X0 = 1,
    X1 = 2,
    X2 = 4,
    X3 = 8,
    X30 = 1073741824,
    X31 = -2147483648,
  };
};

void test() {
  if (!(((Foo::X0 | Foo::X1) & (Foo::X30 | Foo::X31)) != 0)) {
    int x = Foo::X0;
    x = Foo::X1 | Foo::X2;
    x &= ~Foo::X3;
  }
}
").cpp

# Check dynamic types
test("
@export
def test(foo dynamic.Foo) dynamic.Bar {
  return foo as dynamic.Bar + 0 as dynamic.Int + 0 as dynamic
}
", "
Bar test(Foo foo) {
  return (Bar)foo + (Int)0 + 0;
}
").cpp

# Check bit shifts
test("
@export
def test(x int) {
  x = x << x
  x = x >> x
  x = x >>> x
  x <<= x
  x >>= x
  x >>>= x
}
", "
namespace in_int {
  int unsignedRightShift(int self, int x);
}

void test(int x) {
  x = x << x;
  x = x >> x;
  x = in_int::unsignedRightShift(x, x);
  x <<= x;
  x >>= x;
  x = in_int::unsignedRightShift(x, x);
}

int in_int::unsignedRightShift(int self, int x) {
  return (int)((unsigned)self >> x);
}
").cpp

# Test lambda conversion with scope capture and currying
test("
@export
class Foo {
  var value = 0

  def test(x int) int {
    var y = 0
    var f = (a int) => => (b int) => => value + x + y + a + b
    return f(1)()(2)()
  }
}
", "
struct Foo;
struct FooTestEnv;
struct FooTestLambda;
struct FooTestLambda1;
struct FooTestLambda2;
struct FooTestLambda3;

struct Foo {
  int test(int x);
  Foo();

  int value;
};

struct FooTestEnv {
  FooTestEnv();

  Foo *self;
  int x;
  int y;
};

struct FooTestLambda : Fn1<Fn0<Fn1<Fn0<int> *, int> *> *, int> {
  FooTestLambda(FooTestEnv *env);
  virtual Fn0<Fn1<Fn0<int> *, int> *> *run(int a);

  int a;
  FooTestEnv *env;
};

struct FooTestLambda1 : Fn0<Fn1<Fn0<int> *, int> *> {
  FooTestLambda1(FooTestEnv *env, FooTestLambda *lambda);
  virtual Fn1<Fn0<int> *, int> *run();

  FooTestEnv *env;
  FooTestLambda *lambda;
};

struct FooTestLambda2 : Fn1<Fn0<int> *, int> {
  FooTestLambda2(FooTestEnv *env, FooTestLambda *lambda);
  virtual Fn0<int> *run(int b);

  int b;
  FooTestEnv *env;
  FooTestLambda *lambda;
};

struct FooTestLambda3 : Fn0<int> {
  FooTestLambda3(FooTestEnv *env, FooTestLambda *lambda, FooTestLambda2 *lambda1);
  virtual int run();

  FooTestEnv *env;
  FooTestLambda *lambda;
  FooTestLambda2 *lambda1;
};

int Foo::test(int x) {
  FooTestEnv *env = new FooTestEnv();
  env->self = this;
  env->x = x;
  env->y = 0;
  Fn1<Fn0<Fn1<Fn0<int> *, int> *> *, int> *f = new FooTestLambda(env);
  return f->run(1)->run()->run(2)->run();
}

Foo::Foo() {
  this->value = 0;
}

FooTestEnv::FooTestEnv() {
}

FooTestLambda::FooTestLambda(FooTestEnv *env) {
  this->env = env;
}

Fn0<Fn1<Fn0<int> *, int> *> *FooTestLambda::run(int a) {
  this->a = a;
  return new FooTestLambda1(this->env, this);
}

FooTestLambda1::FooTestLambda1(FooTestEnv *env, FooTestLambda *lambda) {
  this->env = env;
  this->lambda = lambda;
}

Fn1<Fn0<int> *, int> *FooTestLambda1::run() {
  return new FooTestLambda2(this->env, this->lambda);
}

FooTestLambda2::FooTestLambda2(FooTestEnv *env, FooTestLambda *lambda) {
  this->env = env;
  this->lambda = lambda;
}

Fn0<int> *FooTestLambda2::run(int b) {
  this->b = b;
  return new FooTestLambda3(this->env, this->lambda, this);
}

FooTestLambda3::FooTestLambda3(FooTestEnv *env, FooTestLambda *lambda, FooTestLambda2 *lambda1) {
  this->env = env;
  this->lambda = lambda;
  this->lambda1 = lambda1;
}

int FooTestLambda3::run() {
  return this->env->self->value + this->env->x + this->env->y + this->lambda->a + this->lambda1->b;
}
").cpp

# Test lambda conversion at global scope
test("
var f = (x int) => (y int) => x + y

@entry
def test int {
  return f(1)(2)
}
", "
struct Lambda;
struct LambdaRunLambda;

struct Lambda : Fn1<Fn1<int, int> *, int> {
  Lambda();
  virtual Fn1<int, int> *run(int x);

  int x;
};

struct LambdaRunLambda : Fn1<int, int> {
  LambdaRunLambda(Lambda *lambda);
  virtual int run(int y);

  Lambda *lambda;
};

Fn1<Fn1<int, int> *, int> *f = new Lambda();

int main() {
  return f->run(1)->run(2);
}

Lambda::Lambda() {
}

Fn1<int, int> *Lambda::run(int x) {
  this->x = x;
  return new LambdaRunLambda(this);
}

LambdaRunLambda::LambdaRunLambda(Lambda *lambda) {
  this->lambda = lambda;
}

int LambdaRunLambda::run(int y) {
  return this->lambda->x + y;
}
").cpp

# Check for a crash with nested loop handling
test("
@export
def test {
  var foo = 0
  var bar = () int => {
    while true {
      for baz in [1] {
        return foo + baz
      }
    }
  }
  bar()
}
", "
struct TestEnv;
struct TestLambda;

struct TestEnv {
  TestEnv();

  int foo;
};

struct TestLambda : Fn0<int> {
  TestLambda(TestEnv *env);
  virtual int run();

  TestEnv *env;
};

void test() {
  TestEnv *env = new TestEnv();
  env->foo = 0;
  Fn0<int> *bar = new TestLambda(env);
  bar->run();
}

TestEnv::TestEnv() {
}

TestLambda::TestLambda(TestEnv *env) {
  this->env = env;
}

int TestLambda::run() {
  while (true) {
    for (int baz : *new List<int>({1})) {
      return this->env->foo + baz;
    }
  }
}
").cpp

# Check that recursive lambdas work
test("
@export
def main {
  var f fn()
  f = => f()
  f()
}
", "
struct MainEnv;
struct MainLambda;

struct MainEnv {
  MainEnv();

  FnVoid0 *f;
};

struct MainLambda : FnVoid0 {
  MainLambda(MainEnv *env);
  virtual void run();

  MainEnv *env;
};

void main() {
  MainEnv *env = new MainEnv();
  env->f = nullptr;
  env->f = new MainLambda(env);
  env->f->run();
}

MainEnv::MainEnv() {
}

MainLambda::MainLambda(MainEnv *env) {
  this->env = env;
}

void MainLambda::run() {
  this->env->f->run();
}
").cpp

# Check for a crash in captured variable substitution
test("
@export
def main {
  var x = [1]
  var f = => x
  for y in x {}
  f()
}
", "
struct MainEnv;
struct MainLambda;

struct MainEnv {
  MainEnv();

  List<int> *x;
};

struct MainLambda : Fn0<List<int> *> {
  MainLambda(MainEnv *env);
  virtual List<int> *run();

  MainEnv *env;
};

void main() {
  MainEnv *env = new MainEnv();
  env->x = new List<int>({1});
  Fn0<List<int> *> *f = new MainLambda(env);

  for (int y : *env->x) {
  }

  f->run();
}

MainEnv::MainEnv() {
}

MainLambda::MainLambda(MainEnv *env) {
  this->env = env;
}

List<int> *MainLambda::run() {
  return this->env->x;
}
").cpp

# Test break statements inside a switch
test("
@export
def test(x int, y bool) {
  while true {
    switch x {
      case 0 {
        if y {
          break
        }
      }
    }
  }
}
", "
void test(int x, bool y) {
  while (true) {
    switch (x) {
      case 0: {
        if (y) {
          goto label;
        }
        break;
      }
    }
  }
  label:;
}
").cpp

# Avoid emitting an empty anonymous enum to avoid a clang warning
test("
@export {
  flags Foo {
    FOO # Comment 1

    # Comment 2
    BAR
    BAZ
  }
  flags Bar {}
}
", "
struct Foo {
  enum {
    // Comment 1
    FOO = 1,

    // Comment 2
    BAR = 2,
    BAZ = 4,
  };
};

struct Bar {
};
").cpp

# Check for accidental mutation of argument types during globalization
test("
class Foo {
  def test(foo Foo) {}
}

def foo {
  var visit fn(Foo)
  visit = foo => visit(foo)
}

@export
def test {
  Foo.new.test(null)
  foo
}
", "
struct Foo;
struct FooEnv;
struct FooLambda;

struct Foo {
  static void test(Foo *self, Foo *foo);
  Foo();
};

struct FooEnv {
  FooEnv();

  FnVoid1<Foo *> *visit;
};

struct FooLambda : FnVoid1<Foo *> {
  FooLambda(FooEnv *env);
  virtual void run(Foo *foo);

  FooEnv *env;
};

void foo() {
  FooEnv *env = new FooEnv();
  env->visit = nullptr;
  env->visit = new FooLambda(env);
}

void test() {
  Foo::test(new Foo(), (Foo *)nullptr);
  foo();
}

void Foo::test(Foo *self, Foo *foo) {
}

Foo::Foo() {
}

FooEnv::FooEnv() {
}

FooLambda::FooLambda(FooEnv *env) {
  this->env = env;
}

void FooLambda::run(Foo *foo) {
  this->env->visit->run(foo);
}
").cpp.globalizeAllFunctions

# Test dead code elimination and imports
test("
@import {
  var a fn()
  def b(x fn(int))
}

var c fn(int, int)
def d(x fn(int, int, int)) {}

@export
def test {
  a = => {}
  b(x => {})
  c = (x, y) => {}
  d((x, y, z) => {})
}
", "
struct TestLambda;
struct TestLambda1;
struct TestLambda2;
struct TestLambda3;

struct TestLambda : FnVoid0 {
  TestLambda();
  virtual void run();
};

struct TestLambda1 : FnVoid1<int> {
  TestLambda1();
  virtual void run(int x);
};

struct TestLambda2 : FnVoid2<int, int> {
  TestLambda2();
  virtual void run(int x, int y);
};

struct TestLambda3 : FnVoid3<int, int, int> {
  TestLambda3();
  virtual void run(int x, int y, int z);
};

FnVoid2<int, int> *c = nullptr;

void d(FnVoid3<int, int, int> *x) {
}

void test() {
  a = new TestLambda();
  b(new TestLambda1());
  c = new TestLambda2();
  d(new TestLambda3());
}

TestLambda::TestLambda() {
}

void TestLambda::run() {
}

TestLambda1::TestLambda1() {
}

void TestLambda1::run(int x) {
}

TestLambda2::TestLambda2() {
}

void TestLambda2::run(int x, int y) {
}

TestLambda3::TestLambda3() {
}

void TestLambda3::run(int x, int y, int z) {
}
").cpp

# Test lambda conversion in loops
test("
@export
def test(list List<int>) {
  (=> list)()
  for i in list {
    (=> list.append(i))()
  }
}
", "
struct TestEnv;
struct TestLambda;
struct TestEnv1;
struct TestLambda1;

struct TestEnv {
  TestEnv();

  List<int> *list;
};

struct TestLambda : Fn0<List<int> *> {
  TestLambda(TestEnv *env);
  virtual List<int> *run();

  TestEnv *env;
};

struct TestEnv1 {
  TestEnv1(TestEnv *env);

  int i;
  TestEnv *env;
};

struct TestLambda1 : FnVoid0 {
  TestLambda1(TestEnv *env, TestEnv1 *env1);
  virtual void run();

  TestEnv *env;
  TestEnv1 *env1;
};

void test(List<int> *list) {
  TestEnv *env = new TestEnv();
  env->list = list;
  new TestLambda(env)->run();

  for (int i : *env->list) {
    TestEnv1 *env1 = new TestEnv1(env);
    env1->i = i;
    new TestLambda1(env, env1)->run();
  }
}

TestEnv::TestEnv() {
}

TestLambda::TestLambda(TestEnv *env) {
  this->env = env;
}

List<int> *TestLambda::run() {
  return this->env->list;
}

TestEnv1::TestEnv1(TestEnv *env) {
  this->env = env;
}

TestLambda1::TestLambda1(TestEnv *env, TestEnv1 *env1) {
  this->env = env;
  this->env1 = env1;
}

void TestLambda1::run() {
  env1->env->list->append(this->env1->i);
}
").cpp.skip # Not working yet

# Constructors on dynamic types shouldn't need parentheses
test("
@export
def test {
  dynamic.Foo.new
  dynamic.Foo.new()
  dynamic.Foo.new.test
  dynamic.Foo.new().test
  dynamic.Foo<dynamic.Bar>.new.test
  dynamic.Foo<dynamic.Bar>.new().test

  var a = dynamic.Foo.new
  var b = dynamic.Foo.new()
  var c = dynamic.Foo.new.test
  var d = dynamic.Foo.new().test
  var e = dynamic.Foo<dynamic.Bar>.new.test
  var f = dynamic.Foo<dynamic.Bar>.new().test
}
", "
void test() {
  new Foo();
  new Foo();
  new Foo()->test;
  new Foo()->test;
  new Foo<Bar>()->test;
  new Foo<Bar>()->test;
  void *a = new Foo();
  void *b = new Foo();
  void *c = new Foo()->test;
  void *d = new Foo()->test;
  void *e = new Foo<Bar>()->test;
  void *f = new Foo<Bar>()->test;
}
").cpp

  }
}
